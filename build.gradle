apply plugin: 'com.github.ben-manes.versions'

buildscript {
  // maps to this commit: https://android.googlesource.com/platform/prebuilts/studio/layoutlib/+/b169460c8473a1e87aafa01da881556682fa4f86
  // which runs on JDK 11
  ext.layoutLibPrebuiltSha = 'ae77d65'

  ext.versions = [
    kotlin: '1.4.31',
    agp: '4.1.2',
    layoutlib: "4.1.0-$layoutLibPrebuiltSha", // "should" be similar to versions.agp
    androidTools: '27.1.2', // agp + 23.0.0 = androidTools
    jcodec: '0.2.5',
    moshi: '1.11.0',
    bytebuddy: '1.10.21',
    releasedPaparazzi: '0.8.0-cash01'
  ]

  ext.deps = [
    plugins: [
      android: "com.android.tools.build:gradle:${versions.agp}",
      kotlin: "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
      publish: 'com.vanniktech:gradle-maven-publish-plugin:0.14.2',
    ],
    kotlin: [
      stdlib: [
        jdk8: "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
      ]
    ],
    kotlinx: [
      coroutines: [
        core: 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2',
      ],
    ],
    tools: [
      common: "com.android.tools:common:${versions.androidTools}",
      layoutlib: "com.android.tools.layoutlib:layoutlib-api:${versions.androidTools}",
      sdkcommon: "com.android.tools:sdk-common:26.6.4",
    ],
    kxml2: 'kxml2:kxml2:2.3.0',
    androidx: [
      annotations: 'androidx.annotation:annotation:1.1.0'
    ],
    layoutlib: [
      native: [
        jdk11: "app.cash.paparazzi:layoutlib-native-jdk11:${versions.layoutlib}",
        macOsX: "app.cash.paparazzi:native-macosx:${versions.layoutlib}",
        windows: "app.cash.paparazzi:native-win:${versions.layoutlib}",
        linux: "app.cash.paparazzi:native-linux:${versions.layoutlib}",
      ]
    ],
    okio: 'com.squareup.okio:okio:2.8.0',
    moshi: [
      core: "com.squareup.moshi:moshi:${versions.moshi}",
      adapters: "com.squareup.moshi:moshi-adapters:${versions.moshi}",
      kotlinCodegen: "com.squareup.moshi:moshi-kotlin-codegen:${versions.moshi}",
    ],
    jcodec: [
      core: "org.jcodec:jcodec:${versions.jcodec}",
      javase: "org.jcodec:jcodec-javase:${versions.jcodec}"
    ],
    bytebuddy: [
      core: "net.bytebuddy:byte-buddy:${versions.bytebuddy}",
      agent: "net.bytebuddy:byte-buddy-agent:${versions.bytebuddy}",
    ],
    grgit: 'org.ajoberstar.grgit:grgit-gradle:4.1.0',
    guava: 'com.google.guava:guava:30.1-jre',
    junit: 'junit:junit:4.13.2',
    truth: 'com.google.truth:truth:1.1.2',
    assertj: 'org.assertj:assertj-core:3.19.0',
    burst: 'com.squareup.burst:burst-junit4:1.2.0',
  ]

  repositories {
    mavenCentral()
    google()
    jcenter()
    mavenLocal()
    gradlePluginPortal()
  }

  dependencies {
    classpath deps.plugins.kotlin
    classpath deps.plugins.android
    classpath deps.plugins.publish
    classpath deps.grgit
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.36.0'
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.18'
    classpath "app.cash.paparazzi:paparazzi-gradle-plugin:${versions.releasedPaparazzi}"
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
    maven {
      url 'https://dl.bintray.com/kotlin/dokka'
    }
    mavenLocal()
    gradlePluginPortal()
  }

  group = GROUP
  version = VERSION_NAME

  tasks.withType(Test).configureEach {
    testLogging {
      events "failed"
      exceptionFormat "full"
      showExceptions true
      showStackTraces true
      showCauses true
    }
  }

  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

tasks.wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
